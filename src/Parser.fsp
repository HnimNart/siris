
%{

let p0 = (0,0)

open Microsoft.FSharp.Text.Parsing
open AbSyn

(* parse-error function *)
let mutable ErrorContextDescriptor : string = ""

let parse_error_rich =
  Some (fun (ctxt: ParseErrorContext<_>) ->
    ErrorContextDescriptor <-
      match ctxt.CurrentToken with
      | None -> "At beginning of input\n"
      | Some token -> sprintf "at token %A\n" token
  )

%}

%token <int*(int*int)> NUM
%token <string*(int*int)> ID VAR
%token <(int*int)> IF THEN ELSE FI
%token <(int*int)> UNTIL REPEAT
%token <(int*int)> SWAP LOCAL DELOCAL
%token <(int*int)> CALL UNCALL
%token <(int*int)> PROCEDURE END
%token <(int*int)> INT
%token <(int*int)> VARDEC LBRACKET RBRACKET
%token <(int*int)> PLUS MINUS MULT DIV MOD
%token <(int*int)> CEQ LTH OR EQ AND NOT
%token <(int*int)> LPAR RPAR SEMIC EOF
%token <(int*int)> LCURLY RCURLY COMMA
%token <(int*int)> READ PRINT
%token <(int*int)> PASSIGN MASSIGN

%left OR
%left AND
%left CEQ LTH
%left PLUS MINUS
%left MULT DIV MOD
%right NOT

%start Prog
%type <AbSyn.Program> Prog
%type <AbSyn.ProcDec List> Procs
%type <AbSyn.ProcDec> Proc
%type <AbSyn.Statement List> Statements
%type <AbSyn.Statement> Statement
%type <AbSyn.Exp> Exp
%type <AbSyn.Type> Type

%%

Prog      : Declarations SEMIC Statements Procs  { $1, $3, $4}
;

Declarations: Declaration COMMA Declarations
                                  { $1 ::  $3 }
             | Declaration        { $1 ::  [] }
;

Declaration: VAR                { IntVar(fst $1, snd $1)}
;

Procs     : PROCEDURE Proc  Procs  { $2 :: $3 }
          | PROCEDURE Proc         { $2 :: [] }
          | EOF                    { [] }
;

Proc      : ID LPAR Params RPAR Statements END
                                   { ProcDec (fst $1, $3,  $5, snd $1) }
          | ID LPAR RPAR Statements END
                                   { ProcDec (fst $1, [], $4 , snd $1)}
;

Params    : VAR COMMA Params { Param (fst $1) :: $3}
          | VAR              { Param (fst $1) :: []}
;

Statements : Statement SEMIC Statements {$1 :: $3}
           | Statement                  {$1 :: []}
           |                            { [] }
;

Statement : VAR PASSIGN Exp
                  { PlusAssignment ((fst $1), $3, $2) }
          | VAR MASSIGN Exp
                  { MinusAssignment ((fst $1), $3, $2) }
          | IF LPAR Exp RPAR THEN Statements ELSE Statements FI LPAR Exp RPAR
                  { If ($3, $6, $8, $11 , $1) }
          | REPEAT Statements UNTIL LPAR Exp RPAR
                  { Repeat ($2, $5, $1)}
          | CALL ID LPAR Params RPAR
                  { Call (fst $2, $4, $1)}
          | CALL ID LPAR RPAR
                  { Call (fst $2, [], $1)}
          | UNCALL ID LPAR Params RPAR
                  { Uncall (fst $2, $4, $1)}
          | UNCALL ID LPAR RPAR
                  { Uncall (fst $2, [], $1)}
          | PRINT LPAR VAR RPAR
                  { Print (fst $3, $1)}
          | READ LPAR VAR RPAR
                  { Read (fst $3, $1)}
          | LOCAL VAR EQ Exp
                   { Local (fst $2, $4, $1)}
          | DELOCAL VAR EQ Exp
                   { Delocal (fst $2, $4, $1)}
          | VAR SWAP VAR
                   { Swap (fst $1, fst $3, $2)}
;

Type      : INT        { AbSyn.Int }
;


Exp : NUM            { Constant (IntVal (fst $1), snd $1) }
    | VAR            { Var $1 }
    | Exp PLUS  Exp  { Plus ($1, $3, $2)    }
    | Exp MINUS Exp  { Minus($1, $3, $2)    }
    | Exp MULT  Exp  { Times($1, $3, $2)    }
    | Exp DIV   Exp  { Divide($1, $3, $2)   }
    | Exp MOD   Exp  { Modulo($1, $3, $2)   }
    | Exp CEQ   Exp  { Equal ($1, $3, $2)   }
    | Exp OR    Exp  { Or ($1, $3, $2)      }
    | Exp AND Exp    { And($1, $3, $2)      }
    | Exp LTH   Exp  { Less ($1, $3, $2)    }
    | NOT Exp        { Not  ($2, $1)    }
    | LPAR Exp RPAR  { $2 }
;

%%
